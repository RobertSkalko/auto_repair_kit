buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '+'
    }
}


plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "me.modmuss50.mod-publish-plugin" version "${mod_publish_version}"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

apply from: 'https://raw.githubusercontent.com/RobertSkalko/ModResources/refs/heads/main/1.20.1/common.gradle'

repositories {
    maven { url = "https://maven.theillusivec4.top/" } // curios
}

dependencies {
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
}

publishMods {
    file = jar.archiveFile
    changelog = file("CHANGELOG.txt").text
    type = "${release_type}".startsWith("release") ? STABLE : ("${release_type}".startsWith("beta") ? BETA : ALPHA)
    modLoaders.add("forge")
    displayName = "${mod_archive_name}-${minecraft_version}-v${mod_version}"

    curseforge {
        accessToken = file("key.properties").text
        projectId = "${curse_id}"
        minecraftVersions.add("${minecraft_version}")
        minecraftVersions.add("1.20")
        requires { slug = "library-of-exile" }
    }
    modrinth {
        accessToken = file("modrinth_key.properties").text
        projectId = "${modrinth_id}"
        minecraftVersions.add("${minecraft_version}")
        minecraftVersions.add("1.20")
        requires { slug = "library-of-exile" }
    }
}


